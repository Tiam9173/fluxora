import java.util.Properties

plugins {
    id("com.android.application")
    id("kotlin-android")
    id("dev.flutter.flutter-gradle-plugin")
}

val localProperties = Properties().apply {
    rootProject.file("local.properties").takeIf { it.exists() }?.inputStream()?.use { load(it) }
}

val mStoreFile = file("keystore.jks")
val mStorePassword: String? = localProperties.getProperty("storePassword")?.takeIf { it.isNotBlank() }
val mKeyAlias: String? = localProperties.getProperty("keyAlias")?.takeIf { it.isNotBlank() }
val mKeyPassword: String? = localProperties.getProperty("keyPassword")?.takeIf { it.isNotBlank() }

val isReleaseBuild = gradle.startParameter.taskNames.any { it.contains("Release", ignoreCase = true) }
if (isReleaseBuild && (!mStoreFile.exists() || mStoreFile.length() == 0L || mStorePassword == null || mKeyAlias == null || mKeyPassword == null)) {
    throw GradleException("Android Release Signing Error: KEYSTORE, KEY_ALIAS, STORE_PASSWORD, or KEY_PASSWORD configurations are missing. Fallback to debug signing is strictly prohibited.")
}

android {
    namespace = "io.fluxora.app"
    compileSdk = 36
    ndkVersion = "28.2.13676358"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        applicationId = "io.fluxora.app"
        minSdk = 26
        targetSdk = 36
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        create("release") {
            storeFile = mStoreFile
            storePassword = mStorePassword ?: ""
            keyAlias = mKeyAlias ?: ""
            keyPassword = mKeyPassword ?: ""
        }
    }

    buildTypes {
        debug {
            isMinifyEnabled = false
            applicationIdSuffix = ".debug"
        }
        release {
            isMinifyEnabled = true
            isDebuggable = false
            signingConfig = signingConfigs.getByName("release")
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    applicationVariants.all {
        outputs.all {
            (this as? com.android.build.gradle.internal.api.ApkVariantOutputImpl)?.versionCodeOverride = flutter.versionCode
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation(project(":core"))
    implementation("com.google.code.gson:gson:2.10.1")
    implementation("com.android.tools.smali:smali-dexlib2:3.0.9") {
        exclude(group = "com.google.guava", module = "guava")
    }
    implementation("androidx.core:core-splashscreen:1.0.1")
}

configurations.all {
    resolutionStrategy {
        eachDependency {
            if (requested.group == "androidx.datastore") useVersion("1.1.2")
        }
    }
}
